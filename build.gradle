plugins {
	id 'java'
	id 'eclipse'
	id "maven"
	id "maven-publish"
	id "com.jfrog.bintray" version "1.8.4"
	id 'co.riiid.gradle' version '0.4.2'

	id 'com.github.ben-manes.versions' version '0.21.0'
	id 'com.github.johnrengelman.shadow' version '5.1.0'

	id 'synapticloop.documentr' version '2.9.0'
	id "synapticloop.copyrightr" version "1.2.1"
}

group = 'synapticloop'
archivesBaseName = 'recipease'
description = """A recipe book creator from json -> PDF"""

version = '1.2.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	implementation 'org.json:json:20160810'
	implementation 'synapticloop:simplelogger:2.0.0'
	implementation 'synapticloop:simpleusage:1.1.2'
	implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.3'
	implementation 'org.apache.xmlgraphics:fop:2.3'
	implementation 'synapticloop:templar:1.4.4'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'

	testCompile 'junit:junit:4.12'

	testRuntime 'junit:junit:4.12'
}

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'org.slf4j:slf4j-api:1.7.13',
					'xml-apis:xml-apis:2.0.2',
					'org.json:json:20160212'

		eachDependency {
			DependencyResolveDetails details ->
				if (details.requested.group == 'junit') {
					details.useVersion '4.12'
				}
		}
	}
}



jar {
	manifest {
		attributes("Main-Class": "synapticloop.recipease.Main",
				"Implementation-Version": version)
	}
}

/*
 *
 * Publish to bintray
 *
 */


def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

 publishing {
	 publications {
		 Synapticloop(MavenPublication) {
			 from components.java
			 artifact sourcesJar
			 artifact javadocJar
 
			 groupId 'synapticloop'
			 artifactId 'recipease'
 
			 pom.withXml {
				 configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
					 asNode().dependencies[0].dependency.find {
						 it.artifactId[0].text() == dep.moduleName &&
								 it.groupId[0].text() == dep.moduleGroup
					 }.scope[0].value = 'compile'
				 }
			 }
		 }
	 }
 }
 
 bintray {
	 user = System.getenv('BINTRAY_USER')
	 key = System.getenv('BINTRAY_PASSWORD')
 
	 publications = [ 'Synapticloop' ]
 
	 publish = true
 
	 pkg {
		 repo = 'maven'
		 name = 'recipease'
	 }
 
 }
 
 github {
	 owner = 'synapticloopltd'
	 repo = archivesBaseName
	 if(System.getenv('GITHUB_TOKEN')) {
		 token = System.getenv('GITHUB_TOKEN')
	 }
	 tagName = version
	 name = version
	 assets = [
		 'build/libs/' + archivesBaseName + '-' + version + '-all.jar'
	 ]
 }

//assemble.finalizedBy(project.tasks.documentr)
assemble.finalizedBy(project.tasks.shadowJar)

